apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'groovy'
apply plugin: 'eclipse'

slf4jVersion = '1.6.3'
jettyVersion = '7.1.6.v20100715'
springVersion = "3.1.0.RELEASE"
springSecurityVersion = '3.1.0.RELEASE'
description = 'Spring Security Samples - SecureMail'
version = '1.0.0.CI-SNAPSHOT'
repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom integrationTestCompile, testRuntime
    }
    ['commons-logging','log4j'].each { groupName -> all*.exclude group: groupName }
}

sourceSets {
    integrationTest {
        groovy.srcDir file('src/it/groovy')
        resources.srcDir file('src/it/resources')
        compileClasspath = sourceSets.main.classes + sourceSets.test.classes + configurations.integrationTestCompile
        runtimeClasspath = classes + compileClasspath + configurations.integrationTestRuntime
    }
}

eclipseClasspath {
    plusConfigurations += configurations.integrationTestRuntime
}
eclipseWtpComponent {
    contextPath = 'mail'
}
dependencies {
    groovy 'org.codehaus.groovy:groovy:1.8.2'

    providedCompile 'javax.servlet:servlet-api:2.5@jar'

    compile "org.springframework:spring-webmvc:$springVersion",
            "org.springframework:spring-jdbc:$springVersion",
            'org.scala-lang:scala-library:2.9.1',
            "org.slf4j:slf4j-api:$slf4jVersion",
            "org.slf4j:log4j-over-slf4j:$slf4jVersion",
            "org.slf4j:jul-to-slf4j:$slf4jVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "javax.servlet:jstl:1.2",
            "javax.validation:validation-api:1.0.0.GA",
            "org.hibernate:hibernate-validator:4.2.0.Final",
            "org.springframework.security:spring-security-core:$springSecurityVersion",
            "org.springframework.security:spring-security-taglibs:$springSecurityVersion"

    runtime "opensymphony:sitemesh:2.4.2",
            "com.h2database:h2:1.3.160",
            'cglib:cglib-nodep:2.2.2',
            'ch.qos.logback:logback-classic:0.9.30',
            "org.springframework.security:spring-security-config:$springSecurityVersion",
            "org.springframework.security:spring-security-web:$springSecurityVersion"

    testCompile "org.springframework:spring-test:$springVersion",
                "org.easytesting:fest-assert:1.4",
                "junit:junit:4.8"

    integrationTestCompile 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.7.0',
                           'org.codehaus.geb:geb-spock:0.6.0',
                           'org.codehaus.geb:geb-core:0.6.0',
                           "org.eclipse.jetty:jetty-server:$jettyVersion",
                           "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    integrationTestCompile('org.spockframework:spock-core:0.5-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

task integrationTest(type: Test, dependsOn: [jar,jettyRunWar]) {
    testClassesDir = sourceSets.integrationTest.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    jettyRunWar.daemon = true
}
build.dependsOn integrationTest

[jettyRun, jettyRunWar]*.configure {
    contextPath = "/mail"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-3'
}